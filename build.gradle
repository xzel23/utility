plugins {
  id "com.github.spotbugs" version "1.6.0"
  id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

/////////////////////////////////////////////////////////////////////////////
	group              = 'com.dua3.utility'
	version            = '0.11.2'
	
	ext.moduleName     = project.group 
	ext.scm            = 'https://gitlab.com/com.dua3.public/utility.git'
	ext.repo           = 'public'
	ext.description    = 'utilites and helper classes'
	
	ext.developerId    = 'xzel23'
	ext.developerName  = 'Axel Howind'
	ext.developerEmail = 'axel@dua3.com'
/////////////////////////////////////////////////////////////////////////////

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    // Log4J2
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
    
    // JUnit
    testImplementation 'junit:junit:4.12'
}

sourceCompatibility = 8
targetCompatibility = 8

// === BINTRAY === >

bintray {
    user = findProperty('BINTRAY_USER') ?: 'NONE'
    key = findProperty('BINTRAY_KEY') ?: 'NONE'

    configurations = ['archives']

    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = false //[Default: false] Whether version should be auto published after an upload    
    override = false //[Default: false] Whether to override version artifacts already published    

    pkg {
        repo = project.repo
        name = project.group
        userOrg = findProperty('BINTRAY_USER_ORG') ?: 'NONE'
        licenses = ['Apache-2.0']
        vcsUrl = project.scm

	    version {
	        name = project.version
	        desc = project.name+version
	        released  = new Date()
	        vcsTag = project.version
	    }
    }
}

// === JIGSAW === >

def moduleInfo = sourceSets.main.allJava.matching { 
    include '**/module-info.java'
}

def hasModuleInfo = !moduleInfo.isEmpty()

if (hasModuleInfo) {
    // check Java version
    if (!JavaVersion.current().isJava9Compatible()) {
        logger.warn('At least Java 9 is needed if a module-info.java file is present. Build will probably fail!')
    }
    
    // if sourceCompatibility is < Java9, exclude module-info.java from the
    // main sourceSet and compile it seperately with compatibility set to Java9
    if (!sourceCompatibility.isJava9Compatible()) {
        task compileModuleInfo (type: JavaCompile) {
            doFirst {
                println 'Compiling module-info.java in Java 9 compatibility mode.'

                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'ALL-SYSTEM',
                    '-d', sourceSets.main.output.classesDirs.asPath
                ]
            }
            
            inputs.property("moduleName", project.moduleName)

            sourceCompatibility = 9
            targetCompatibility = 9

            source = sourceSets.main.java.srcDirs
            classpath = sourceSets.main.compileClasspath 
            include '**/module-info.java'
            destinationDir = sourceSets.main.output.classesDir
        }
		
        sourceSets.main.java { 
            exclude '**/module-info.java'
        }
     
		compileModuleInfo.dependsOn compileJava
		assemble.dependsOn compileModuleInfo
    }         
}

// === MAVEN publication === >

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id    project.developerId
            name  project.developerName
            email project.developerEmail
        }
    }
    
    scm {
       url project.scm
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        UtilityPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.group)
                root.appendNode('url', project.scm)
                root.children().last() + pomConfig
            }
        }
    }
}

// === SPOTBUGS === >

spotbugs {
    sourceSets = [sourceSets.main]
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

// === ARTIFACTS === >

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
	archives jar
    archives sourcesJar
    archives javadocJar
}

// === DEFAULT TASKS === >

defaultTasks 'build', 'publishToMavenLocal', 'install'

// Copyright (c) 2019 Axel Howind
//
// This software is released under the MIT License.
// https://opensource.org/licenses/MIT

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.dua3.cabe', name: 'cabe-gradle-plugin', version: '0.4.1'
    }
}

plugins {
    id 'com.adarshr.test-logger' version '3.2.0' apply false
    id 'com.github.spotbugs' version '5.0.6' apply false
    id 'org.kordamp.gradle.errorprone' version '0.47.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'idea'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'org.kordamp.gradle.errorprone'
    apply plugin: 'com.dua3.cabe.cabe-gradle-plugin'

    /////////////////////////////////////////////////////////////////////////////
    group = 'com.dua3.utility'
    version = '10-BETA21-SNAPSHOT'

    ext.scm = 'https://gitlab.com/com.dua3/lib/utility.git'
    ext.repo = 'public'

    ext.developerId = 'axh'
    ext.developerName = 'Axel Howind'
    ext.developerEmail = 'axh@dua3.com'
    ext.organization = 'dua3'
    ext.organizationUrl = 'https://www.dua3.com'
    /////////////////////////////////////////////////////////////////////////////

    ext {
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        java_version = '17'
        junit_version = '5.8.2'
        cabe_version = '0.4.1'
    }

    sourceCompatibility = java_version
    targetCompatibility = java_version

    repositories {
        // Maven local Repository
        mavenLocal()
        
        // GitLab Packages
        if (System.properties.containsKey('gitlabPrivateToken')) {
            maven {
                url "https://gitlab.com/api/v4/projects/24900343/packages/maven"
                name "GitlabPrivate"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.properties.get('gitlabPrivateToken')
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        } else if (System.getenv("CI_JOB_TOKEN")!=null) {
            maven {
                url "https://gitlab.com/api/v4/projects/24900343/packages/maven"
                name "GitlabPrivate"
                credentials(HttpHeaderCredentials) {
                    name = 'Job-Token'
                    value = System.getenv("CI_JOB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }

        // Maven Central Repository
        mavenCentral()
    }

    // dependencies
    dependencies {
        // Cabe (source annotations)
        compileOnly "com.dua3.cabe:cabe-annotations:${cabe_version}"

        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/java/main/")
            testOutputDir = file("$buildDir/classes/java/test/")
        }
    }

    test {
        useJUnitPlatform()
    }

    testlogger {
        theme 'standard-parallel'
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.addBooleanOption('html5', true)
    }

    // === MAVEN publication === >

    // Create the pom configuration:
    def pomConfig = {
        licenses {
            license {
                name "MIT"
                url "https://opensource.org/licenses/MIT"
            }
        }
        developers {
            developer {
                id project.developerId
                name project.developerName
                email project.developerEmail
                organization project.organization
                organizationUrl project.organizationUrl
            }
        }

        scm {
            url project.scm
        }
    }

    // Create the publication with the pom configuration:
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version version
                from components.java
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.appendNode('name', project.name)
                    root.appendNode('url', project.scm)
                    root.children().last() + pomConfig
                }
            }
        }

        repositories {
            // Sonatype OSSRH
            maven {
                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = isReleaseVersion ? releaseRepo : snapshotRepo
                credentials {
                    username = System.properties.get('ossrhUsername')
                    password = System.properties.get('ossrhPassword')
                }
            }
        }
    }

    // === sign artifacts
    signing {
        sign publishing.publications.mavenJava
    }

    // === SPOTBUGS ===
    spotbugs.excludeFilter = rootProject.file('spotbugs-exclude.xml')

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    // === ERRORPRONE ===
    config {
        quality {
            errorprone.setEnabled(true)
            errorprone.setErrorProneVersion('2.11.0')
        }
    }
    
    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
            disable("UnnecessaryParentheses")
            disable("EqualsGetClass")
            disable("StringSplitter")
        }
    }

    // === PUBLISHING ===
    tasks.withType(PublishToMavenRepository) {
        dependsOn publishToMavenLocal
    }

    tasks.withType(Jar) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

}

// === DEFAULT TASKS === >
defaultTasks 'build', 'publishToMavenLocal', 'dependencyUpdates'
